action (GetBuyer) {
  description (Prompt user to enter personal data)
  type (Constructor)
  collect {
    //Order is a structure with transaction feature that has the current buyer and product properties
    input (order) {
      type (Order)
      min (Required)
    }
    //This is the list of the buyer fields which user wants to updated.
    input (fieldsToUpdate) {
      type (self.RequiredField)
      min (Required) max (Many)
      default-init {
        intent {
          goal-set: self.RequiredField {
            self.RequiredField(firstName)
            self.RequiredField(lastName)
            self.RequiredField(emailInfo)
            self.RequiredField(phoneInfo)
          }
         }
      }
    }
    //the buyer fields that can be updated by user
    input (firstName) {
      type (contact.FirstName)
      min (Optional) max (One)
    }
    input (lastName) {
      type (contact.LastName)
      min (Optional) max (One)
    }
    input (emailAddress) {
      type (contact.EmailAddress)
      min (Optional) max (One)
    }
    input (phoneNumber) {
      type (contact.PhoneNumber)
      min (Optional) max (One)
    }
    //compute the new buyer information by calling self library
    computed-input (buyer) {
      type (Buyer)
      min (Required)
      compute {
        intent {
          goal: self.GetProfile
          value: self.Profile$expr(order.buyer)
          //do not force a prompt if a field value is available. make sure to include all fields.
          if (exists(firstName) || exists(lastName) || exists(emailAddress) || exists(phoneNumber)) {
            value: $expr(firstName)
            value: $expr(lastName)
            value: $expr(emailAddress)
            value: $expr(phoneNumber)
          } else {
            value: self.ForcePrompt(true)
          }
          value: $expr(fieldsToUpdate)
        }
      }
    }
  }
  output (Buyer) {
    evaluate { $expr(buyer) }
  }
}